import com.android.build.gradle.LibraryPlugin
import com.android.build.gradle.api.LibraryVariant

apply plugin: 'java'
evaluationDependsOn(':core')

repositories {
    def androidHome = System.getenv("ANDROID_HOME")

    // android repository is added in order to satisfy robolectric 2.4 dependencies
    maven {
        url "$androidHome/extras/android/m2repository/"
    }
}

dependencies {
    Project appModule = project(':core')

    LibraryVariant applicationDebugVariant = appModule.android.libraryVariants.matching({
        it.name.equals('debug')
    }).getAt(0)
    LibraryPlugin plugin = appModule.plugins.findPlugin('com.android.library') as LibraryPlugin

    // this lets you navigate from tests to sources and makes sure the app is build before the test
    testCompile appModule

    // this adds the dependencies of the application
    testCompile applicationDebugVariant.javaCompile.classpath

    // this adds the class files of the application
    testCompile applicationDebugVariant.javaCompile.outputs.files

    // this adds android classes
    testCompile files(plugin.bootClasspath)

    testCompile 'junit:junit:4.11'
    testCompile 'org.robolectric:robolectric:2.4'
}

tasks.withType(Test) {
    scanForTestClasses = false
    include "**/*Should.class"
    include "**/*Test.class"
    include "**/*Tests.class"
}
